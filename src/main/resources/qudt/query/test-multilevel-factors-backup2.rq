PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX kind: <http://qudt.org/vocab/quantitykind/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Bind
# ?u1 ?e1 ... ?u5 ?e5 (must be either untyped or xsd:integer!!)
# to get the derived unit as ?unit that has exactly those (transitive) factor units
SELECT ?unit ?cnt ?matched
WHERE
{
SELECT ?unit (count(*) as ?cnt) (sum(?matchedUnit) as ?matched) ?requestedUnits
    (REPLACE(GROUP_CONCAT(?matchedUnitExpTags; separator=""),"\\s+","") AS ?allMatchedTags)
    (REPLACE(GROUP_CONCAT(?unmatchedUnitExpTags; separator=""),"\\s+","") AS ?allUnmatchedTags)
    (REPLACE(?allMatchedTags, "><",">|<") as ?regex)
    (REPLACE(?allUnmatchedTags, ?regex, "") AS ?replaced)
#SELECT *
{
    #VALUES ( ?u1 ?e1 ?u2 ?e2 ?u3 ?e3 ?u4 ?e4 ?u5 ?e5 ) { ( unit:M 1 unit:KiloGM 1 unit:SEC -2 unit:M -2 unit:HZ -1 )}
    #VALUES ( ?u1 ?e1 ?u2 ?e2) { ( unit:M -2 unit:KiloGM 1 )}
    VALUES ( ?u1 ?e1 ?u2 ?e2 ?u3 ?e3 ?u4 ?e4) { ( unit:M 1 unit:KiloGM 1 unit:SEC -2 unit:M -2)}
        {
            SELECT
                ?unit
                (GROUP_CONCAT(STR(?midFactor); separator= ";") as ?midFactors)
                (GROUP_CONCAT(?midExponent; separator= ";") as ?exponents)
                (GROUP_CONCAT(?unitExpTag; separator= " ") as ?unitExpTags)
                ?endUnit
            WHERE
            {

                ?unit qudt:factorUnit/(qudt:unit/qudt:factorUnit)* ?midFactor .
                ?midFactor (qudt:unit/qudt:factorUnit)*  ?endFactor .
                ?midFactor
                    qudt:unit ?midUnit ;
                    qudt:exponent ?midExponent .
                ?endFactor
                    qudt:unit ?endUnit ;
                    qudt:exponent ?endExponent .
                BIND(CONCAT("<",STR(?midUnit), ",", STR(?midExponent),">") AS ?unitExpTag)


            }
            GROUP BY ?unit ?endUnit ?endExponent
        }

        BIND(REPLACE(?exponents,"(-?\\d)(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?", "$1") AS ?es1)
        BIND(REPLACE(?exponents,"(-?\\d)(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?", "$3") AS ?es2)
        BIND(REPLACE(?exponents,"(-?\\d)(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?", "$5") AS ?es3)
        BIND(REPLACE(?exponents,"(-?\\d)(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?", "$7") AS ?es4)
        BIND(REPLACE(?exponents,"(-?\\d)(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?(;(-?\\d))?", "$9") AS ?es5)
        BIND(IF(?es1 = "", 1, STRDT(?es1, xsd:integer)) AS ?en1)
        BIND(IF(?es2 = "", 1, STRDT(?es2, xsd:integer)) AS ?en2)
        BIND(IF(?es3 = "", 1, STRDT(?es3, xsd:integer)) AS ?en3)
        BIND(IF(?es4 = "", 1, STRDT(?es4, xsd:integer)) AS ?en4)
        BIND(IF(?es5 = "", 1, STRDT(?es5, xsd:integer)) AS ?en5)
        BIND(?en1 * ?en2 * ?en3 * ?en4 * ?en5 AS ?exponent)
        BIND(IF(
                    (BOUND(?u1) && BOUND(?e1) && ?u1 = ?endUnit && ?e1 = ?exponent)
                    || (BOUND(?u2) && BOUND(?e2) && ?u2 = ?endUnit && ?e2 = ?exponent)
                    || (BOUND(?u3) && BOUND(?e3) && ?u3 = ?endUnit && ?e3 = ?exponent)
                    || (BOUND(?u4) && BOUND(?e4) && ?u4 = ?endUnit && ?e4 = ?exponent)
                    || (BOUND(?u5) && BOUND(?e5) && ?u5 = ?endUnit && ?e5 = ?exponent)
                    || (BOUND(?u6) && BOUND(?e6) && ?u6 = ?endUnit && ?e6 = ?exponent)
                    || (BOUND(?u7) && BOUND(?e7) && ?u7 = ?endUnit && ?e7 = ?exponent)
                    ,1, 0
               ) AS ?matchedUnit)
        BIND(
            IF(BOUND(?u1),1,0)
            + IF(BOUND(?u2),1,0)
            + IF(BOUND(?u3),1,0)
            + IF(BOUND(?u4),1,0)
            + IF(BOUND(?u5),1,0)
            + IF(BOUND(?u6),1,0)
            + IF(BOUND(?u7),1,0)
            AS ?requestedUnits
        )
        BIND(IF(?matchedUnit = 1, ?unitExpTags, "") AS ?matchedUnitExpTags)
        BIND(IF(?matchedUnit = 1, "", ?unitExpTags) AS ?unmatchedUnitExpTags)

}
GROUP BY ?unit ?requestedUnits
HAVING (?matched = ?requestedUnits && ?replaced = "" )
ORDER BY ?unit
}
